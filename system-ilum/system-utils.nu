export def pacman-tracked-configs [] {
  ^pacman -Qii
    | ^jc --pacman
    | from json
    | select -i name backup_files
    | flatten -a
    | where backup_files != null
    | update backup_files {|p| $p.backup_files | parse '{file} [{status}]'}
    | flatten -a
}

export def pacman-owned-files [] {
  ^pacman -Ql
    | lines
    | parse '{owned_by} {file}'
    | update file {|| str trim --right --char '/'}
    | uniq
}

export def glob-root-files [] {
  glob /mnt/rootfs/@/**/*
    | each {|| str substring 13..}
    | where {($in | str length) > 0}
    | where {not ($in starts-with /usr/share/mime/)} # Generated by a pacman hook
    | where {not ($in starts-with /etc/ca-certificates/extracted/)} # Generated
    | where {not ($in starts-with /etc/ssl/certs/)} # Generated
    | where {not ($in starts-with /etc/pacman.d/gnupg/)} # Generated
    | str trim --right --char '/'
    | uniq
    | wrap file
    | insert in_fs true
}

export def pacman-not-owned-files [] {
  pacman-owned-files | join --right (glob-root-files) file | where owned_by == null
}
