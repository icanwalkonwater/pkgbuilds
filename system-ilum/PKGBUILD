pkgbase=system-ilum
pkgname=(system-ilum-skeleton system-ilum-base system-ilum-utils system-ilum-full)
pkgver=1.0.1
pkgrel=1

pkgdesc='System configuration'
url='https://github.com/icanwalkonwater/pkgbuilds'
arch=('any')
license=('MIT')

source=('sudoers.d-allow-sudo-group')
sha256sums=('81576086c069a2c098b510553a20c6b44b42c954d7d359991ec90db475142486')

package_system-ilum-skeleton() {
  pkgdesc='Skeleton for ilum'
  depends=()

  # Mountpoint for nix subvolume
  mkdir -p "$pkgdir"/nix
  # Mountpoint for the subvolume containing the swapfile
  mkdir -p "$pkgdir"/swap

  # Mountpoint for root btrfs subvolume
  mkdir -p "$pkgdir"/mnt/rootfs
  # Mountpoint for big SSD
  mkdir -p "$pkgdir"/mnt/fastass
  # Mountpoint for random stuff like usb sticks
  mkdir -p "$pkgdir"/mnt/stuff
}

package_system-ilum-base() {
  pkgdec='Base packages for ilum'
  depends=(
    # Skeleton
    'system-ilum-skeleton'
    # Needed to boot
    'base' 'linux' 'linux-firmware' 'btrfs-progs'
    'efibootmgr' 'grub' 'os-prober' 'grub-theme-minegrub-world-selection-git'
    # Maintenance
    'reflector' 'pacman-contrib' 'smartmontools' 'snapper'
    # AUR
    'base-devel' 'paru'
    # Dotfiles
    'stow'

    # Drivers
    'nvidia-open' 'nvidia-utils'
    'intel-ucode' 'intel-media-driver'
    'libva-nvidia-driver' 'libva-utils' 'vulkan-tools'

    # Shell
    'fish' 'nushell' 'starship' 'bat' 'htop' 'nvtop' 'tree' 'sudo' 'git' 'lazygit'
    'man-db' 'man-pages'
    'neovim' 'fzf' 'ripgrep'
    'zellij' 'fbset' 'fastfetch'
    'openssh'
  )

  install -Dm0700 sudoers.d-allow-sudo-group "$pkgdir"/etc/sudoers.d/allow-sudo-group
}

package_system-ilum-utils() {
  pkgdesc='Utility scripts for ilum'
  depends=()
}

package_system-ilum-full() {
  pkgdesc='Full desktop for ilum'
  depends=(
    'system-ilum-base' 'system-ilum-utils'

    # Destkop setup
    # Display Manager
    'ly'
    # Session Manager
    'uwsm' 'libnewt'
    # Window Manager & friends
    'hyprland' 'hyprpolkitagent' 'hyprlock' 'hyprpaper' 'hyprsunset'
    # Screenshot
    'hyprshot' 'xdg-desktop-portal-hyprland'
    'hyprpicker' 'qt5-wayland'
    # Status bar
    'waybar'
    # Notification daemon
    'dunst'
    # Terminal emulator
    'alacritty'
    # Application launcher
    'rofi-wayland'
    # File manager
    'ranger'
    # Image viewer
    'feh'
    # Sound
    'pipewire' 'wireplumber' 'pipewire-pulse' 'pavucontrol'
    # Flatpak
    'flatpak'
    # Steam
    'steam' 'lib32-nvidia-utils' 'lib32-systemd'
    # Fonts
    'ttf-jetbrains-mono-nerd' 'ttf-liberation'
    # Bit random
    'nwg-look' 'papirus-icon-theme' 'brightnessctl'
  )
}
