pkgbase=system-ilum
pkgname=(system-ilum-skeleton system-ilum-base system-ilum-utils system-ilum-full)
pkgver=1.3.3
pkgrel=1

pkgdesc='System configuration'
url='https://github.com/icanwalkonwater/pkgbuilds'
arch=('any')
license=('MIT')

source=(
  'etc-hostname'
  'etc-vconsole.conf'
  'etc-locale.conf'
  'ilum-grub.nu'
  'ilum-report.nu'
  'ilum-patch.nu'
  'etc-systemd-network-89-ethernet.network'
  'sudoers.d-allow-sudo-group'
  'etc-makepkg.d-parallel.conf'
  'etc-pacreport.conf'
  'pacman-hook-ilum-base-patch.hook'
  'pacman-hook-ilum-full-patch.hook'
  'etc-pacman.conf.patch'
  'etc-xdg-reflector-reflector.conf.patch'
  'etc-locale.gen.patch'
  'etc-ly-config.ini.patch'
  'etc-default-grub.patch'
  'etc-mkinitcpio.conf.patch'
)

package_system-ilum-skeleton() {
  pkgdesc='Skeleton for ilum'
  depends=()

  # Mountpoint for nix subvolume
  mkdir -p "$pkgdir"/nix
  # Mountpoint for the subvolume containing the swapfile
  mkdir -p "$pkgdir"/swap

  # Mountpoint for root btrfs subvolume
  mkdir -p "$pkgdir"/mnt/rootfs
  # Mountpoint for big SSD
  mkdir -p "$pkgdir"/mnt/fastass
  # Mountpoint for random stuff like usb sticks
  mkdir -p "$pkgdir"/mnt/stuff
}

package_system-ilum-base() {
  pkgdec='Base packages for ilum'
  depends=(
    # Skeleton
    'system-ilum-skeleton'
    # Needed to boot
    'base' 'linux' 'linux-firmware' 'btrfs-progs' 'dosfstools'
    'efibootmgr' 'grub' 'os-prober' 'grub-theme-minegrub-world-selection-git'
    # Maintenance
    'reflector' 'pacman-contrib' 'smartmontools' 'patch' 'pacutils'
    # AUR
    'base-devel' 'paru'
    # Dotfiles
    'stow'

    # Drivers
    'nvidia-open' 'nvidia-utils'
    'intel-ucode' 'intel-media-driver'
    'libva-nvidia-driver' 'libva-utils' 'vulkan-tools'

    # Shell
    'fish' 'nushell' 'starship' 'bat' 'htop' 'nvtop' 'tree' 'sudo' 'git' 'lazygit' 'jq' 'jc'
    'man-db' 'man-pages'
    'neovim' 'fzf' 'ripgrep'
    'zellij' 'fbset' 'fastfetch'
    'openssh' 'sshfs'
    'unzip'
    'imagemagick' 'ffmpeg'
  )
  install='system-ilum-full.install'

  # Basic stuff
  install -Dm0644 etc-hostname "$pkgdir"/etc/hostname
  ln -sf ../usr/share/zoneinfo/Europe/Paris "$pkgdir"/localtime
  install -Dm0644 etc-vconsole.conf "$pkgdir"/etc/vconsole.conf
  install -Dm0644 etc-locale.conf "$pkgdir"/etc/locale.conf
  
  # Network
  ln -sf ../run/systemd/resolve/stub-resolv.conf "$pkgdir"/etc/resolv.conf
  install -Dm0644 etc-systemd-network-89-ethernet.network "$pkgdir"/etc/systemd/network/89-ethernet.network

  # Sudo
  install -Dm0700 sudoers.d-allow-sudo-group "$pkgdir"/etc/sudoers.d/allow-sudo-group
  chmod 750 "$pkgdir"/etc/sudoers.d/

  # Archlinux stuff
  install -Dm0644 etc-makepkg.d-parallel.conf "$pkgdir"/etc/makepkg.conf.d/99-parallel.conf
  install -Dm0644 etc-pacreport.conf "$pkgdir"/etc/pacreport.conf

  # Patch setup.
  install -Dm0755 ilum-patch.nu "$pkgdir"/usr/bin/ilum-patch
  install -Dm0644 pacman-hook-ilum-base-patch.hook "$pkgdir"/usr/share/libalpm/hooks/ilum-base-patch.hook

  # Patches
  install -Dm0644 etc-pacman.conf.patch "$pkgdir"/usr/share/ilum/patches/etc-pacman.conf.patch
  install -Dm0644 etc-xdg-reflector-reflector.conf.patch "$pkgdir"/usr/share/ilum/patches/etc-xdg-reflector-reflector.conf.patch
  install -Dm0644 etc-locale.gen.patch "$pkgdir"/usr/share/ilum/patches/etc-locale.gen.patch
  install -Dm0644 etc-default-grub.patch "$pkgdir"/usr/share/ilum/patches/etc-default-grub.patch
  install -Dm0644 etc-mkinitcpio.conf.patch "$pkgdir"/usr/share/ilum/patches/etc-mkinitcpio.conf.patch

  # Grub
  install -Dm0755 ilum-grub.nu "$pkgdir"/usr/bin/ilum-grub
}

package_system-ilum-utils() {
  pkgdesc='Utility scripts for ilum'
  depends=('system-ilum-base')

  install -Dm0755 ilum-report.nu "$pkgdir"/usr/bin/ilum-report
}

package_system-ilum-full() {
  pkgdesc='Full desktop for ilum'
  depends=(
    'system-ilum-base' 'system-ilum-utils'

    # Destkop setup
    # Display Manager
    'ly'
    # Session Manager
    'uwsm' 'libnewt'
    # Window Manager & friends
    'hyprland' 'hyprpolkitagent' 'hyprlock' 'hyprpaper' 'hyprsunset'
    # Screenshot
    'hyprshot' 'xdg-desktop-portal-hyprland'
    'hyprpicker' 'qt5-wayland'
    # Status bar
    'waybar'
    # Notification daemon
    'dunst'
    # Base apps
    'rofi-wayland' 'alacritty' 'ranger' 'feh' 'mpv'
    # Sound
    'pipewire' 'wireplumber' 'pipewire-pulse' 'pavucontrol'
    # External apps stuff
    'nix' 'flatpak' 'docker' 'k9s' 'kubectl'
    # Steam
    'steam' 'lib32-nvidia-utils' 'lib32-systemd'
    # Fonts
    'ttf-jetbrains-mono-nerd' 'ttf-liberation'
    # Bit random
    'nwg-look' 'papirus-icon-theme' 'brightnessctl' 'wev'
    'vulkan-caps-viewer-wayland'
  )
  install='system-ilum-full.install'

  # Additionnal patches
  install -Dm0644 pacman-hook-ilum-full-patch.hook "$pkgdir"/usr/share/libalpm/hooks/ilum-full-patch.hook
  install -Dm0644 etc-ly-config.ini.patch "$pkgdir"/usr/share/ilum/patches/etc-ly-config.ini.patch
}

sha256sums=('5a93eced3b28435ffbd7ac8450c4a3dedcb7d49f793c81a3afe68dc45c49d35a'
            '2c09b43e51de87a82ff5eeb1c599ce25800b8d34810f14044bf73644e9f7b742'
            '033252d5100cbb0028d7c15e075f63e4eb5ff7f1a923cd5cb547bcfd6b024f5b'
            '87395b842b7c19d461328e68b73d835e05b3d1d9c4e47c0aefe4e21b1326d74e'
            'c4511e0c0d28a5cf862631f0ce72febe93adf6ffba2f7851254356191d76c3d2'
            '80a75366a6c16ae6fd44988cbffecfd185b3b7db85b5455e76df4781f34c9d51'
            '23dd289c12332bb8821363bcdd2d593b3171f1d5e2c5d9b9f888dbe9e981e3d4'
            '81576086c069a2c098b510553a20c6b44b42c954d7d359991ec90db475142486'
            '6804efe7de2a1b4cebbd36d4d7c92a1b43bffc1688c98f642bfd53303bfb1f3c'
            '59badeaff799f030f62a24e749168adc02cd0e232f145e5f288579a6234e40b2'
            '41cf16a331f1b3cf91f22d66df7be28ffe445747ca14ca20a2fa3ce2f1ddd024'
            '648bfb7917803a234c8b7413fd771589e40646e2d17e31bc5d2f489e6355f20f'
            'b69640be1aa0f148678e969a675fb29f166e13d74e779c8f4d025a42a2ce3504'
            '9d676d4e2cd9acaead7b7156c8bb25286fbeac619b84739e9e73406049402115'
            'e9bf5423102855dc6f3eed8daff2f627bd8b1d7e490bbcbb336c08a72168b1cf'
            '8d52bb79c7419e979db81e480ad36a93cdf6bb05f4c694226d37512fb66ac5a7'
            '08f0ac07d8dfd0e07b4efb09929db2f6a76e28c5b5f01a88581adc1b5db337e9'
            'aae18e369ed76e0a1aaeb7c333349b7dfb89923dc961c9fbf493a356b5da0059')
